Процедура ЗаписатьСообщениеСИзменениями(Каталог) Экспорт

	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "-------- Выгрузка в узел " + Строка(ЭтотОбъект) + " ------------";
	// Сформировать имя временного файла.
	ИмяФайла = Каталог + "Message" + СокрЛП(ПланыОбмена.Мобильные.ЭтотУзел().Код) + "_" + СокрЛП(Ссылка.Код) + ".xml";

	// Создать объект записи XML.
	// *** ЗаписьXML-документов.
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ИмяФайла);
	ЗаписьXML.ЗаписатьОбъявлениеXML();

	// *** Инфраструктура сообщений.
	ЗаписьСообщения = ПланыОбмена.СоздатьЗаписьСообщения();
	ЗаписьСообщения.НачатьЗапись(ЗаписьXML, Ссылка);
	Сообщение.Текст = Сообщение.Текст + Символы.ПС + " Номер сообщения: " + ЗаписьСообщения.НомерСообщения;

	// Получить выборку измененных данных.
	// *** Механизм регистрации изменений.
	ВыборкаИзменений = ПланыОбмена.ВыбратьИзменения(ЗаписьСообщения.Получатель, ЗаписьСообщения.НомерСообщения);
	
	Пока ВыборкаИзменений.Следующий() Цикл
		Данные = ВыборкаИзменений.Получить();
		// Записать данные в сообщение *** XML-сериализация.
		ЗаписатьДанные(ЗаписьXML, Данные);
	КонецЦикла;

	ЗаписьСообщения.ЗакончитьЗапись();
	ЗаписьXML.Закрыть();

	Сообщение.Текст = Сообщение.Текст + Символы.ПС + "-------- Конец выгрузки ------------";
	Сообщение.Сообщить();

КонецПроцедуры

Процедура ЗаписатьДанные(ЗаписьXML, Данные) Экспорт
	
	Если ТипЗнч(Данные) = Тип("СправочникОбъект.ОсновныеСредства") Тогда

		// Записываем элемент справочника вручную.
		ЗаписатьXMLОсновноеСредство(ЗаписьXML, Данные);

	Иначе
		
		// Записываем данные с помощью стандартного метода.
		ЗаписатьXML(ЗаписьXML, Данные);

	КонецЕсли

КонецПроцедуры

Процедура ЗаписатьXMLОсновноеСредство(ЗаписьXML, ОсновноеСредство )

	// Записываем начало элемента XML
	ЗаписьXML.ЗаписатьНачалоЭлемента("CatalogObject.ОсновныеСредства.Вручную");

	// Ссылка
	ЗаписатьXML(ЗаписьXML, ОсновноеСредство.Ссылка, "Ref", НазначениеТипаXML.Явное);

	// ЭтоГруппа
	ЗаписатьXML(ЗаписьXML, ОсновноеСредство.ЭтоГруппа, "IsFolder", НазначениеТипаXML.Явное);

	// Родитель
	ЗаписатьXML(ЗаписьXML, ОсновноеСредство.Родитель, "Parent", НазначениеТипаXML.Явное);

	// Наименование
	ЗаписатьXML(ЗаписьXML, ОсновноеСредство.Наименование, "Description", НазначениеТипаXML.Явное);

	// Записываем конец элемента

	ЗаписьXML.ЗаписатьКонецЭлемента();

КонецПроцедуры

Процедура ПрочитатьСообщениеСИзменениями(Каталог) Экспорт

	// Сформировать имя файла.
	ИмяФайла = Каталог + "Message" + СокрЛП(Ссылка.Код) + "_" + СокрЛП(ПланыОбмена.Мобильные.ЭтотУзел().Код) + ".xml";
	Файл = Новый Файл(ИмяФайла);
	Если Не Файл.Существует() Тогда
		Возврат;
	КонецЕсли;

	// *** Чтение документов XML        
	// Попытаться открыть файл.
	ЧтениеXML = Новый ЧтениеXML;
	Попытка
		ЧтениеXML.ОткрытьФайл(ИмяФайла);
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Невозможно открыть файл обмена данными.";
		Сообщение.Сообщить();
		Возврат;
	КонецПопытки;

	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "-------- Загрузка из " + Строка(ЭтотОбъект) + " ------------";
	Сообщение.Текст = Сообщение.Текст + Символы.ПС + " – Считывается файл " + ИмяФайла;
	
	// Загрузить из найденного файла
	// *** Инфраструктура сообщений.
	ЧтениеСообщения = ПланыОбмена.СоздатьЧтениеСообщения();

	// Читать заголовок сообщения обмена данными – файла XML.
	ЧтениеСообщения.НачатьЧтение(ЧтениеXML);

	// Сообщение предназначено не для этого узла.
	Если ЧтениеСообщения.Отправитель <> Ссылка Тогда
		ВызватьИсключение "Неверный узел";
	КонецЕсли;

	// Удаляем регистрацию изменений для узла отправителя сообщения.
	// *** Служба регистрации изменений.
	ПланыОбмена.УдалитьРегистрациюИзменений(ЧтениеСообщения.Отправитель, ЧтениеСообщения.НомерПринятого);

	// Читаем данные из сообщения *** XML-сериализация.
	Пока ВозможностьЧтенияДанных(ЧтениеXML) Цикл
		// Читаем очередное значение.
		Данные = ПрочитатьДанные(ЧтениеXML);
		// Записать полученные данные.
		Данные.ОбменДанными.Отправитель = ЧтениеСообщения.Отправитель;
		Данные.ОбменДанными.Загрузка = Истина;
		Данные.Записать();
	КонецЦикла;

	ЧтениеСообщения.ЗакончитьЧтение();
	ЧтениеXML.Закрыть();
	УдалитьФайлы(ИмяФайла);

	Сообщение.Текст = Сообщение.Текст + Символы.ПС + "-------- Конец загрузки ------------";
	Сообщение.Сообщить();

КонецПроцедуры

Функция ВозможностьЧтенияДанных(ЧтениеXML)

	// Получаем тип данных XML, который может быть считан в данный момент
	ТипXML = ПолучитьXMLТип(ЧтениеXML);
	
	Если ТипXML = Неопределено Тогда
	
		Возврат Ложь;
	
	КонецЕсли;

	Если (ТипXML.ИмяТипа = "CatalogObject.Основныесредства.Вручную" И ТипXML.URIПространстваИмен = "") Тогда

		Возврат Истина;

	КонецЕсли;
	
	Возврат ВозможностьЧтенияXML(ЧтениеXML);

КонецФункции

Функция ПрочитатьДанные(ЧтениеXML)

	ТипXML = ПолучитьXMLТип(ЧтениеXML);
	Если ТипXML = Неопределено Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;

	Если ТипXML.ИмяТипа = "CatalogObject.ОсновныеСредства.Вручную" И ТипXML.URIПространстваИмен = "" Тогда
		// Пытаемся прочесть значение справочника "ОсновныеСредства".
		Возврат ПрочитатьXMLОсновныеСредства(ЧтениеXML);

	КонецЕсли;
	
	// Пытаемся прочесть значение из объекта ЧтениеXML стандартным образом.
	Возврат ПрочитатьXML(ЧтениеXML);
	
КонецФункции // ПрочитатьДанные(ЧтениеXML)

Функция ПрочитатьXMLОсновныеСредства(ЧтениеXML)

	Если ЧтениеXML.ТипУзла <> ТипУзлаXML.НачалоЭлемента Тогда
		ВызватьИсключение "Ошибка чтения XML";
	КонецЕсли;

	// Чтение следующего узла.
	ЧтениеXML.Прочитать();

	// Чтение ссылки на элемент справочника.
	ОсновноеСредствоСсылка = ПрочитатьXML(ЧтениеXML);

	Если ТипЗнч(ОсновноеСредствоСсылка) <> Тип("СправочникСсылка.ОсновныеСредства") Тогда
		ВызватьИсключение "Ошибка чтения XML";
	КонецЕсли;

	// Пытаемся создать объект по полученной ссылке.
	ОсновноеСредство = ОсновноеСредствоСсылка.ПолучитьОбъект();
	// Чтение признака группы.
	ЭтоГруппа = ПрочитатьXML(ЧтениеXML);

	Если ОсновноеСредство <> Неопределено Тогда
		Если ОсновноеСредство.ЭтоГруппа <> ЭтоГруппа Тогда
			ВызватьИсключение "Некорректные данные";
		КонецЕсли;
	Иначе
		// Создаем элемент справочника.
		Если ЭтоГруппа = Истина Тогда
			ОсновноеСредство = Справочники.ОсновныеСредства.СоздатьГруппу();
		Иначе
			ОсновноеСредство = Справочники.ОсновныеСредства.СоздатьЭлемент();
		КонецЕсли;

		// Устанавливаем значение ссылки для нового объекта
		ОсновноеСредство.УстановитьСсылкуНового(ОсновноеСредствоСсылка);
	КонецЕсли;

	// Родитель.
	ОсновноеСредство.Родитель = ПрочитатьXML(ЧтениеXML);
	// Наименование.
	ОсновноеСредство.Наименование = ПрочитатьXML(ЧтениеXML);

	// Проверяем, что текущим узлом является "КонецЭлемента".
	Если ЧтениеXML.ТипУзла <> ТипУзлаXML.КонецЭлемента Тогда
		ВызватьИсключение "Ошибка чтения XML";
	КонецЕсли;

	// Чтение следующего узла для завершения чтения элемента.
	ЧтениеXML.Прочитать();

	Возврат ОсновноеСредство;

КонецФункции